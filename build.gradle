buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "gradle"
            url = "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'// apply false
    id 'org.flywaydb.flyway' version '8.0.0-beta2'
}

repositories {
    mavenCentral()
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        name = 'jitpack-repo'
        url = 'https://jitpack.io'
    }
}

static def loadEnv() {
    def env = [:]
    try {
        def file = new File('.env')
        def scanner = new Scanner(file)
        while (scanner.hasNextLine()) {
            def line = scanner.nextLine()
            if (line.contains('=')) {
                def pair = line.split('=')
                env.put(pair[0], pair.length == 2 ? pair[1] : '')
            }
        }
    } catch (FileNotFoundException ignored) {
    }
    env.putAll(System.getenv())
    return env
}

def env = loadEnv()

dependencies {
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.7'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.6'
    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.7.4'
    implementation group: 'org.hibernate', name: 'hibernate-hikaricp', version: '5.5.7.Final'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.5'
    implementation group: 'io.github.openfeign', name: 'feign-okhttp', version: '11.6'
    implementation group: 'io.github.openfeign', name: 'feign-jackson', version: '11.6'

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
}

shadowJar {
    def googleGson = 'com.google.code.gson'
    def googleGuava = 'com.google.guava'
    def localGoogleGson = 'gg.sbs.api.google.gson'
    def localGoogleGuava = 'gg.sbs.api.google.guava'
    relocate googleGson, localGoogleGson
    relocate googleGuava, localGoogleGuava
}

flyway {
    url = 'jdbc:mariadb://' + env.get('DB_URL') + ':' + env.get('DB_PORT') + '/' + env.get('DB_DATABASE')
    user = env.get('DB_USER')
    password = env.get('DB_PASSWORD')
}